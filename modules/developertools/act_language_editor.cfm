<cfsetting enablecfoutputonly="Yes">
<!---
Copyright (C) by LANshock.com
Released under the GNU General Public License (v2)

$HeadURL$
$LastChangedDate$
$LastChangedBy$
$LastChangedRevision$
--->

<cfparam name="attributes.form_submitted" default="false">
<cfparam name="attributes.file" default="">
<cfparam name="attributes.directory" default="">

<cfset fileDefault = application.lanshock.environment.abspath & attributes.directory & 'lang.default'>

<cfif FileExists(fileDefault)>
	<cffile action="read" file="#fileDefault#" variable="langDefaults">
	<cfparam name="attributes.reference" default="#langDefaults#">
<cfelse>
	<cfparam name="attributes.reference" default="#attributes.file#">
</cfif>

<cfset attributes.langFileReference = application.lanshock.environment.abspath & attributes.directory & attributes.reference>
<cfset attributes.langFile = application.lanshock.environment.abspath & attributes.directory & attributes.file>

<cfif NOT len(attributes.langFile) OR NOT FileExists(attributes.langFile)>
	<cflocation url="#myself##myfusebox.thiscircuit#.language_main&#request.session.urltoken#" addtoken="false">
</cfif>

<cfif attributes.form_submitted>
	<!--- initialise the vars --->
	<cfscript>
		/* using java.util.Properties and java.io.FileOutputStream instead of CFFILE
			automatically converts the special-chars of each language to unicode escaped strings (/xxxx) */
		prop = createObject("java", "java.util.Properties");
		fos = createObject("java", "java.io.FileOutputStream");
		rbHeader = '## generated by: LANshock Developer Tools' & chr(13) & '## generated at: #now()#' & chr(13) & '## please do not modify this file yourself - use the developer tools for changes';
		thisRBfile = attributes.langFile;
	</cfscript>
	
	<!--- populate the property object --->
	<cfloop list="#ListSort(StructKeyList(attributes),'textnocase')#" index="idx">
		<cfif ListFirst(idx,'_') EQ 'element'>
			<!--- <cfset key = Lcase(ListDeleteAt(idx,1,'_'))> --->
			<cfset key = Lcase(right(idx,len(idx)-len('element_')))>
			<!--- remove the extra '_'
				this '_' prevenst coldfusion from auto-validation fields that end with '_date'
				in fact this should only happen with <cfinput> this happens in our tests with <input> too :( --->
			<cfset key = left(key,len(key)-1)>
			<cfset value = attributes[idx]>
			<cfset prop.setProperty(key,value)>
		</cfif>
	</cfloop>
	
	<!--- write the rbFile --->
	<cfscript>
		fos.init(thisRBfile);
		prop.store(fos,rbHeader);
		fos.close();
	</cfscript>
	
	<cflocation url="#myself##myfusebox.thiscircuit#.language_main&directory=#UrlEncodedFormat(attributes.directory)#&#request.session.urltoken#" addtoken="false">
</cfif>

<cfinvoke component="#request.lanshock.environment.componentpath#core._utils.i18n.javaRB" method="getResourceBundle" returnvariable="stRB">
	<cfinvokeargument name="rbFile" value="#attributes.langFile#">
</cfinvoke>

<cfinvoke component="#request.lanshock.environment.componentpath#core._utils.i18n.javaRB" method="getResourceBundle" returnvariable="stRBreference">
	<cfinvokeargument name="rbFile" value="#attributes.langFileReference#">
</cfinvoke>

<cfif ListFirst(attributes.directory,'/') EQ 'core'>
	<cfset prependString = application.lanshock.settings.modulePrefix.core>
<cfelse>
	<cfset prependString = application.lanshock.settings.modulePrefix.module>
</cfif>

<cfif StructKeyExists(application.module,prependString & ListLast(attributes.directory,'/'))>
	<cfset lGlobalModuleLabels = '__globalmodule__name'>

	<cfloop collection="#application.module[prependString & ListLast(attributes.directory,'/')].navigation#" item="idx">
		<cfset sNewLabel = '__globalmodule__navigation__' & application.module[prependString & ListLast(attributes.directory,'/')].navigation[idx].action>
		<cfset lGlobalModuleLabels = ListAppend(lGlobalModuleLabels,sNewLabel)>
	</cfloop>

	<cfloop collection="#application.module[prependString & ListLast(attributes.directory,'/')].securityareas#" item="idx">
		<cfset sNewLabel = '__globalmodule__security__' & idx>
		<cfset lGlobalModuleLabels = ListAppend(lGlobalModuleLabels,sNewLabel)>
	</cfloop>

	<cfloop list="#lGlobalModuleLabels#" index="NavIDX">
		<cfif NOT StructKeyExists(stRBreference,NavIDX)>
			<cfset stRBreference[NavIDX] = ''>
		</cfif>
		<cfif NOT StructKeyExists(stRB,NavIDX)>
			<cfset stRB[NavIDX] = stRBreference[NavIDX]>
		</cfif>
	</cfloop>
</cfif>

<cfdirectory action="list" directory="#GetDirectoryFromPath(attributes.langFile)#" name="qReference" filter="*.properties" sort="name ASC">



<cfsetting enablecfoutputonly="No">