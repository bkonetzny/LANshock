<!---
Copyright (C) by LANshock.com
Released under the GNU General Public License (v2)

$HeadURL: https://lanshock.svn.sourceforge.net/svnroot/lanshock/trunk/core/admin/setup.cfc $
$LastChangedDate: 2007-09-30 14:43:42 +0200 (So, 30 Sep 2007) $
$LastChangedBy: majestixs $
$LastChangedRevision: 103 $
--->

<cfcomponent>
	
	<cffunction name="createConfig" output="false" returntype="void">

		<cfset var sConfig = ''>
		<cfset var sCircuits = ''>
		<cfset var stModules = StructNew()>
		<cfset var idxModule = ''>
		<cfset var sFolder = ''>
		<cfset var bMvcModule = false>
		<cfset var qParsedFiles = 0>
		
		<cfinvoke component="#application.lanshock.oModules#" method="getModules" returnvariable="stModules"/>
		
		<cfset sCircuits = ''>
		<cfloop list="#listSort(StructKeyList(stModules),'textnocase')#" index="idxModule">
			<cfif stModules[idxModule].general.createCircuit>
				<cfset idxModule = lCase(idxModule)>
				<cfset bMvcModule = false>
				<cfif NOT fileExists(expandPath('modules/#idxModule#/circuit.xml.cfm')) AND fileExists(expandPath('modules/#idxModule#/controller/circuit.xml.cfm'))>
					<cfset bMvcModule = true>
				</cfif>
				<cfif NOT bMvcModule>
					<cfset sCircuits = sCircuits & chr(13) & chr(9) & chr(9) & '<circuit alias="#idxModule#" path="modules/#idxModule#/"/>'>
				<cfelse>
					<cfset sCircuits = sCircuits & chr(13) & chr(9) & chr(9) & '<circuit alias="#idxModule#" path="modules/#idxModule#/controller/"/>'>
					<cfset sCircuits = sCircuits & chr(13) & chr(9) & chr(9) & '<circuit alias="m_#idxModule#" path="modules/#idxModule#/model/"/>'>
					<cfset sCircuits = sCircuits & chr(13) & chr(9) & chr(9) & '<circuit alias="v_#idxModule#" path="modules/#idxModule#/view/"/>'>
				</cfif>
			</cfif>
		</cfloop>
		<cfset sCircuits = trim(sCircuits)>

		<cfsavecontent variable="sConfig">
			<cfoutput>
<?xml version="1.0" encoding="UTF-8"?>
<!-- generated by LANshock at #now()# -->
<!DOCTYPE fusebox>
<fusebox>
	<circuits>
		<circuit alias="core_runtime" path="core/runtime/"/>
		<circuit alias="udfs" path="core/_utils/udf/" parent="" />
		#sCircuits#
	</circuits>
	<parameters>		
		<parameter name="fuseactionVariable" value="fuseaction"/>
		<parameter name="defaultFuseaction" value="general.welcome"/>
		<parameter name="mode" value="production"/>
		<parameter name="strictMode" value="true"/>
		<parameter name="password" value="#CreateUUID()#"/>
		<parameter name="characterEncoding" value="utf-8"/>
		<parameter name="debug" value="false"/>
		<parameter name="allowImplicitCircuits" value="true"/>
		<parameter name="queryStringStart" value="/" />
		<parameter name="queryStringSeparator" value="/" />
		<parameter name="queryStringEqual" value="/" />
		<parameter name="parsePath" value="storage/secure/parsed/fusebox/" />
		<parameter name="pluginsPath" value="fusebox5/plugins/" />
		<parameter name="lexiconPath" value="fusebox5/lexicon/" />
	</parameters>
	<globalfuseactions>
		<appinit/>
		<preprocess/>
		<postprocess/>
	</globalfuseactions>
	<plugins>
		<phase name="preProcess">
			<plugin name="AttributesFilter" template="AttributesFilter.cfm"/>
			<plugin name="Security" template="Security.cfm"/>
			<plugin name="LanguageLoader" template="LanguageLoader.cfm"/>
			<plugin name="NestedSettings" template="NestedSettings.cfm"/>
			<plugin name="LayoutHeader" template="LayoutHeader.cfm"/>
		</phase>
		<phase name="preFuseaction"/>
		<phase name="postFuseaction"/>
		<phase name="fuseactionException"/>
		<phase name="postProcess">
			<plugin name="LayoutFooter" template="LayoutFooter.cfm"/>
		</phase>
		<phase name="processError"/>
	</plugins>
</fusebox>
			</cfoutput>
		</cfsavecontent>
		
		<cffile action="write" file="#expandPath('fusebox.xml.cfm')#" output="#trim(sConfig)#" mode="777">
		
		<cfdirectory action="list" name="qParsedFiles" directory="#application.lanshock.oRuntime.getEnvironment().sStoragePath#secure/parsed/fusebox/" filter="*.cfm">
		
		<cfloop query="qParsedFiles">
			<cffile action="delete" file="#qParsedFiles.directory#/#qParsedFiles.name#">
		</cfloop>
	
	</cffunction>

</cfcomponent>